###########################################
###### Workflow for HISAT2 alignment ######
## and processing BAM files for counting ##
###########################################

## - Information about project & samples - ##
# Original fastq files were obtained from the authors.
# SRA Project ID: SRP132477; Bioproject ID: PRJNA433508; Gene Expression Omnibus Series ID: GSE110344
# All sample names (indicated by {sample}) include both the SRA Sample ID (SRS#) and the treatment group - e.g. SRS2926577_C1no.
# Files were initially separated by run (8 runs per sample with the exception of SRS2926581_S1no); run # is listed for applicable files.

# THINGS TO CHANGE
# 1. Consider adding rules for marking/removing duplicates and adding readgroups
# 2. Change to final directory paths

import os

# Config file
configfile: "walker_config.json"

# Tool paths
hisat2_path = "/home/avannan/miniconda3/envs/rodent_addiction/bin/hisat2" # Version 2.2.1
samtools_path = "/home/avannan/miniconda3/envs/rodent_addiction/bin/samtools" # Version 1.7
bamtools_path = "/home/avannan/miniconda3/envs/rodent_addiction/bin/bamtools" # Version 2.5.1

# Directory Variables
# References
hisat2_index = config["hisat2_index"] # Contains 8 files for indexed genome with splice sites and exons identified
# Trimmed FASTQs
trimmed_fastq_dir = config["trimmed_fastq_dir"] # Trimmed FASTQs
# Alignment
hisat2_sams_dir = config["hisat2_sams_dir"] # SAM files generated by HISAT2
hisat2_bams_dir = config["hisat2_bams_dir"] # BAM files converted from HISAT2 SAM files
# Processing (doesn't include marking duplicates or adding read groups)
hisat2_sorted_bams_dir = config["hisat2_sorted_bams_dir"] # Sorted HISAT2 BAM files
hisat2_sorted_bams_stats_dir = config["hisat2_sorted_bams_stats_dir"] # Bamtool stats files for sorted BAMs

######################
## All Output Files ##
######################

rule all:
	input:
		# ALIGNMENT WITH HISAT2
		# HISAT2 paired, trimmed SAM files
		expand(hisat2_sams_dir + "walker_{sample}_hisat2_trim.sam", sample = config["samples_ALL"]),
		# HISAT2 paired, trimmed BAM files
		expand(hisat2_bams_dir + "walker_{sample}_hisat2_trim.bam", sample = config["samples_ALL"]),

		# PROCESSING AFTER HISAT2 ALIGNMENT
		# HISAT2 paired, trimmed, sorted, BAM files and their indexes
		expand(hisat2_sorted_bams_dir + "walker_{sample}_hisat2_trim_sort.bam", sample = config["samples_ALL"]),
		expand(hisat2_sorted_bams_dir + "walker_{sample}_hisat2_trim_sort.bam.bai", sample = config["samples_ALL"]),
		# Stats for paired, trimmed, sorted, HISAT2 BAM files
		expand(hisat2_sorted_bams_stats_dir + "walker_{sample}_hisat2_trim_sort_stats.txt", sample = config["samples_ALL"])

##################
## 03_alignment ##
##################

###########################
## Alignment with HISAT2 ##
###########################

rule hisat2_align:
	input:
		TRIMMED_FQ = trimmed_fastq_dir + "walker_{sample}_trim.fastq"
	output:
		HISAT2_SAM = hisat2_sams_dir + "walker_{sample}_hisat2_trim.sam"
	message: "Mapping {wildcards.sample} reads to {params.HISAT2_INDEX} with HISAT2."
	params:
		hisat2 = hisat2_path,
		HISAT2_INDEX = hisat2_index,
		threads = 8
	shell:
		"""
		{params.hisat2} --dta -p {params.threads} -x {params.HISAT2_INDEX} -U {input.TRIMMED_FQ} -S {output.HISAT2_SAM}
		"""

rule hisat2_sam_to_bam:
	input:
		HISAT2_SAM = hisat2_sams_dir + "walker_{sample}_hisat2_trim.sam"
	output:
		HISAT2_BAM = hisat2_bams_dir + "walker_{sample}_hisat2_trim.bam"
	message: "Converting {input.HISAT2_SAM} to BAM."
	params:
		samtools = samtools_path
	shell:
		"""
		{params.samtools} view -b {input.HISAT2_SAM} > {output.HISAT2_BAM}
		"""

###################
## 04_processing ##
###################

#################################################
## Processing BAM files after HISAT2 alignment ##
#################################################

rule hisat2_sort:
	input:
		HISAT2_UNSORTED_BAM = hisat2_bams_dir + "walker_{sample}_hisat2_trim.bam"
	output:
		HISAT2_SORTED_BAM = hisat2_sorted_bams_dir + "walker_{sample}_hisat2_trim_sort.bam"
	message: "Sorting BAM file {input.HISAT2_UNSORTED_BAM}."
	params:
		bamtools = bamtools_path
	shell:
		"""
		{params.bamtools} sort -in {input.HISAT2_UNSORTED_BAM} -out {output.HISAT2_SORTED_BAM}
		"""

rule hisat2_sorted_stats:
	input:
		HISAT2_SORTED_BAM = hisat2_sorted_bams_dir + "walker_{sample}_hisat2_trim_sort.bam"
	output:
		HISAT2_SORTED_STATS = hisat2_sorted_bams_stats_dir + "walker_{sample}_hisat2_trim_sort_stats.txt"
	params:
		bamtools = bamtools_path
	shell:
		"""
		{params.bamtools} stats -in {input.HISAT2_SORTED_BAM} > {output.HISAT2_SORTED_STATS}
		"""

rule hisat2_index_sorted_bams:
	input:
		HISAT2_SORTED_BAM = hisat2_sorted_bams_dir + "walker_{sample}_hisat2_trim_sort.bam"
	output: hisat2_sorted_bams_dir + "walker_{sample}_hisat2_trim_sort.bam.bai"
	message: "Indexing BAM file {input.HISAT2_SORTED_BAM} with Bamtools."
	params:
		bamtools = bamtools_path
	shell:
		"""
		{params.bamtools} index -in {input.HISAT2_SORTED_BAM}
		"""